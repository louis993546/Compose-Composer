plugins {
    id "app.cash.exhaustive"
    id "com.android.application"
    id "com.google.protobuf" version "0.8.17"
    id "com.squareup.sqldelight"
    id "kotlin-android"
    id "org.jetbrains.kotlin.kapt"
    id "org.jetbrains.kotlin.android"
}

android {
    compileSdk 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.louis993546.composecomposer"
        minSdk 21
        targetSdk 30
        def number = System.getenv("GITHUB_RUN_NUMBER") ?: 1
        versionCode number.toInteger()
        versionName "${number}.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true

        multiDexEnabled true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
        freeCompilerArgs = ["-Xinline-classes"]
    }

    buildFeatures.compose = true

    composeOptions.kotlinCompilerExtensionVersion compose_version

    lintOptions {
        htmlReport true
        sarifReport true
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('debug.keystore')
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobuf_version"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.5.0"
    implementation "androidx.appcompat:appcompat:1.3.0"
    implementation "com.google.android.material:material:1.3.0"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    implementation "androidx.activity:activity-compose:1.3.0-beta01"
    implementation "androidx.navigation:navigation-compose:2.4.0-alpha02"

    //region data store
    implementation "com.google.protobuf:protobuf-javalite:$protobuf_version"
    implementation "androidx.datastore:datastore:1.0.0-beta01"
    //endregion

    //region moshi
    implementation "com.squareup.moshi:moshi:1.12.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.12.0"
    kapt "dev.zacsweers.moshix:moshi-sealed-codegen:0.11.2"
    implementation "dev.zacsweers.moshix:moshi-sealed-runtime:0.12.0"
    //endregion

    //region Accompanist
    implementation "com.google.accompanist:accompanist-glide:$accompanist_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-insets:$accompanist_version"
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"
    //endregion

    //region kotlinx datetime
    implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.2.1"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"
    //endregion

    implementation "com.squareup.sqldelight:android-driver:$sqldelight_version"
    implementation "com.squareup.sqldelight:coroutines-extensions:$sqldelight_version"
    implementation "com.jakewharton.timber:timber:4.7.1"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}
