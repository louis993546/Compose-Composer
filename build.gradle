// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        compose_version = "1.0.0-beta08"
        sqldelight_version = "1.5.0"
        protobuf_version = "3.17.3"
        accompanist_version = "0.11.1"
    }
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://www.jetbrains.com/intellij-repository/releases" }
        maven { url "https://jetbrains.bintray.com/intellij-third-party-dependencies" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.1.0-alpha02"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.20"
        classpath "com.squareup.sqldelight:gradle-plugin:$sqldelight_version"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.17.1"
}

detekt {
    buildUponDefaultConfig = true
    config = files("config/detekt/detekt.yml")
    baseline = file("detekt-baseline.xml")
    allRules = false // skip unstable rules

    input = files(
            "app/src/main/java",
            "app/src/test/java",
            "app/src/androidTest/java",
    )

    reports {
        html.enabled = true
        xml.enabled = true
        sarif.enabled = true
    }
}

tasks.detekt.jvmTarget = "1.8"

task clean(type: Delete) {
    delete rootProject.buildDir
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        // Treat all Kotlin warnings as errors
        allWarningsAsErrors = true

        // for Color moshi adapter (ULong)
        freeCompilerArgs += "-Xopt-in=kotlin.ExperimentalStdlibApi"
    }
}